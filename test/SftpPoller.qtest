#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%new-style
%require-types
%strict-args
%enable-all-warnings

%requires ssh2 >= 1.2
%requires ../qlib/SftpPoller.qm
%requires QUnit
%requires Util

%exec-class SftpPollerTest

class MySftpPoller inherits SftpPoller {
    public {}

    private {
        list l = ();
        bool verbose;
    }

    constructor(SFTPClient sc, hash opts, bool n_verbose) : SftpPoller(sc, opts) {
        verbose = n_verbose;
    }

    nothing singleFileEvent(hash fh) {
        if (verbose)
            printf("GOT FILE: %y\n", fh - "data" + ("data_type": fh.data.type(), "data_size": fh.data.size()));
        # in this case, the polling stop operation will take effect after all the singleFileEvent() calls are made for the polling operation
        stopNoWait();
    }

    nothing postSingleFileEvent(hash fh) {
        l += fh;
    }

    list getFileList() {
        return l;
    }
}

class SftpPollerTest inherits QUnit::Test {
    public {}

    private {
        const Timeout = 10s;

        const MyOpts = Opts + (
            "privkey": "k,private-key=s",
            "mask": "m,mask=s",
            "dir": "d,dir=s"
            );

        SFTPClient sc;

        const ColumnOffset = 25;

        const Files = (
            "a.txt": "abc",
            "b.txt": "bcd",
            "c.txt": "cde",
            "d.php": "def",
            );

        const Result = (
            ("name": "a.txt",
             "size": 3,
             "type": "REGULAR",
             "data": "abc",
            ),
            ("name": "b.txt",
             "size": 3,
             "type": "REGULAR",
             "data": "bcd",
            ),
            ("name": "c.txt",
             "size": 3,
             "type": "REGULAR",
             "data": "cde",
            ),
            );

        const NoRetrievalFiles = (
            "x.txt": "abc",
            );

        const NoRetrievalResult = (
            ("name": "x.txt",
             "size": 3,
             "type": "REGULAR",
            ),
            );
    }

    constructor() : Test("SftpPollerTest", "1.2", \ARGV, MyOpts) {
        addTestCase("SftpPollerTests", \sftpPollerTests());
        addTestCase("SftpPolerRemotePathNegTest", \sftpPollerRemotePathNegTest());

        string uri = shift ARGV ?? sprintf("%s@localhost", getusername());

        sc = new SFTPClient(uri);
        if (m_options.privkey) {
            sc.setKeys(m_options.privkey);
        }
        else {
            if (PlatformOS == "Windows")
                exit(0);

            string sshdir = `echo -n ~/.ssh`;
            if (is_file(sshdir + "/id_rsa"))
                sc.setKeys(sshdir + "/id_rsa");
            else if (is_file(sshdir + "/id_dsa"))
                sc.setKeys(sshdir + "/id_dsa");
        }

        set_return_value(main());
    }

    private usageIntern() {
        TestReporter::usageIntern(ColumnOffset);
        printOption("-k,--private-key=ARG", "set private key to use for authentication", ColumnOffset);
    }

    sftpPollerTests() {
        code info = sub (string msg) {
            if (m_options.verbose)
                printf("INFO: %s\n", msg);
        };
        code detail = sub (string msg) {
            if (m_options.verbose > 1)
                printf("DETAIL: %s\n", msg);
        };
        code debug = sub (string msg) {
            if (m_options.verbose > 2)
                printf("DEBUG: %s\n", msg);
        };

        hash opts = (
            "log_info": info,
            "log_detail": detail,
            "log_debug": debug,
            "mask": "*.txt",
            "writable": True,
            );

        string nd = sprintf("%s/", get_random_string());
        # setup directory
        if (m_options.dir) {
            nd = sprintf("%s/%s/",  m_options.dir, get_random_string());
        } 

        try {
            sc.mkdir(nd);
        } catch (ex) {
            if (ex.desc =~ /LIBSSH2_FX_PERMISSION_DENIED/) {
                stdout.print("Root is not writable, specify writable directory as --dir(-d).\n");
            }
            throw ex;
        }
        
        map sc.putFile($1.value, nd + $1.key), Files.pairIterator();
        on_exit {
            map sc.removeFile($1), Files.keyIterator();
            sc.chdir("..");
            sc.rmdir(nd);
        }

        opts.path = nd;

        MySftpPoller poller(sc, opts, m_options.verbose > 2);
        poller.start();
        poller.waitStop();
        list fl = poller.getFileList();
        # sort by file name
        fl = map $1.(Result[0].keys()), sort(fl, int sub (hash l, hash r) { return l.name <=> r.name; });
        assertEq(fl, Result);

        # testing the data retrieval skipping (one file is enough)
        string nd2 = sprintf("%s/", get_random_string());
        sc.mkdir(nd2);
        map sc.putFile($1.value, nd2 + $1.key), NoRetrievalFiles.pairIterator();
        on_exit {
            map sc.removeFile($1), NoRetrievalFiles.keyIterator();
            sc.chdir("..");
            sc.rmdir(nd2);
        }

        opts.path = nd2;
        opts.skip_file_content_retrieval = True;

        MySftpPoller poller2(sc, opts, m_options.verbose > 2);
        poller2.start();
        poller2.waitStop();
        fl = poller2.getFileList();
        map assertEq(False, exists $1.data), fl;
        # sort by file name
        fl = map $1.(NoRetrievalResult[0].keys()), sort(fl, int sub (hash l, hash r) { return l.name <=> r.name; });
        assertEq(fl, NoRetrievalResult);
    }

    sftpPollerRemotePathNegTest() {
        code info = sub (string msg) {
            if (m_options.verbose)
                printf("INFO: %s\n", msg);
        };
        code detail = sub (string msg) {
            if (m_options.verbose > 1)
                printf("DETAIL: %s\n", msg);
        };
        code debug = sub (string msg) {
            if (m_options.verbose > 2)
                printf("DEBUG: %s\n", msg);
        };

        hash opts = (
            "log_info": info,
            "log_detail": detail,
            "log_debug": debug,
            "mask": "*.txt",
            "writable": True,
            );

        # setup directory
        string nd = sprintf("%s/%s/", tmp_location(), get_random_string());
        sc.mkdir(nd);
        map sc.putFile($1.value, nd + $1.key), Files.pairIterator();
        on_exit {
            map sc.removeFile(nd + $1), Files.keyIterator();
            sc.rmdir(nd);
        }

        opts.path = nd;

        {
            sc.chmod(nd, 0555);
            on_exit sc.chmod(nd, 0777);
            assertThrows("REMOTE-DIR-ERROR", sub () {MySftpPoller poller(sc, opts, m_options.verbose > 2);});
        }
    }
}
