# -*- mode: qore; indent-tabs-mode: nil -*-
#! @file Ssh2Connections.qm SSH2 module connection implementations

/*  Ssh2Connections.qm Copyright 2013 - 2021 Qore Technologies, s.r.o.

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

# minimum required Qore version
%requires qore >= 0.9.5

# require type definitions everywhere
%require-types

# enable all warnings
%enable-all-warnings

%strict-args

%requires(reexport) ssh2
%requires(reexport) ConnectionProvider >= 1.4

%new-style

module Ssh2Connections {
    version = "1.4";
    desc = "user module for providing SSH2 connections";
    author = "David Nichols <david@qore.org>";
    url = "http://qore.org";
    license = "MIT";
    init = sub () {
        ConnectionSchemeCache::registerScheme("ssh", Ssh2ClientConnection::ConnectionScheme);
        ConnectionSchemeCache::registerScheme("sftp", SftpConnection::ConnectionScheme);
    };
}

/** @mainpage Ssh2Connections Module

    @tableofcontents

    @section ssh2connectionsintro Introduction to the Ssh2Connections Module

    The %Ssh2Connections module provides connection definitions for classes
    provided by the \c ssh2 module for the @ref connectionproviderintro "ConnectionProvider"
    module in %Qore.

    To use this module, use \c "%requires Ssh2Connections" in your code.

    This module automatically uses the <a href="../../ssh2/html/index.html">ssh2</a> binary module.

    All the public symbols in the module are defined in the Ssh2Connections namespace.

    Currently the module provides the following classes:
    - @ref Ssh2Connections::AbstractSsh2Connection "AbstractSsh2Connection"
    - @ref Ssh2Connections::SftpConnection "SftpConnection"
    - @ref Ssh2Connections::Ssh2ClientConnection "Ssh2ClientConnection"

    @section ssh2connections_relnotes Ssh2Connections Module Release History

    @subsection ssh2connections_v1_4 Ssh2Connections v1.4
    - implemented support for a data provider scheme cache and rich option information for connections
      (<a href="https://github.com/qorelanguage/qore/issues/4025">issue 4025</a>)

    @subsection ssh2connections_v1_3 Ssh2Connections v1.3
    - removed the obsolete \c Ssh2ClientConnection::getConstructorInfo() and \c SftpConnection::getConstructorInfo()
      methods as connection serialization is a much more elegant and maintainable solution
      (<a href="https://github.com/qorelanguage/qore/issues/3696">issue 3696</a>)

    @subsection ssh2connections_v1_2 Ssh2Connections v1.2
    - new AbstractConnection infrastructure

    @subsection ssh2connections_v1_1 Ssh2Connections v1.1
    - the @ref Ssh2Connections::Ssh2ClientConnection::getConstructorInfo() "Ssh2ClientConnection::getConstructorInfo()"
      and @ref Ssh2Connections::SftpConnection::getConstructorInfo() "SftpConnection::getConstructorInfo()"
      methods were added to allow connections to be created dynamically, potentially in another process from a network
      call (<a href="https://github.com/qorelanguage/qore/issues/2628">issue 2628</a>)

    @subsection ssh2connections_v1_0 Ssh2Connections v1.0
    - the initial version of the Ssh2Connections module
*/

#! the Ssh2Connections namespace contains all the definitions in the Ssh2Connections module
public namespace Ssh2Connections {
#! abstract base class for \c ssh module connections
/** supports the following option:
    - \c "keyfile": a path to a private key file in PEM format to use for key-based authentication
*/
public class AbstractSsh2Connection inherits ConnectionProvider::AbstractConnectionWithInfo {
    #! creates the AbstractSsh2Connection object
    /** @param name the name of the connection
        @param description connection description
        @param url connection URL (potentially with password info)
        @param attributes various attributes. See below
        @param options connection options

        See @ref AbstractConnection::constructor() for \c attributes and \c options reference.
    */
    constructor(string name, string description, string url, hash<auto> attributes = {}, hash<auto> options = {})
        : AbstractConnectionWithInfo(name, description, url, attributes, options) {
    }
}

#! class for SSH2 client session connections; returns an object of class @ref Qore::SSH2::SSH2Client
/** supports the following option:
    - \c "keyfile": a path to a private key file in PEM format to use for key-based authentication (environment variables can be prefixed with \c "$" in the path (or optionally enclosed in curly brackets as in \c "${var}") and will be substituted with the value of the environment variable)
*/
public class Ssh2ClientConnection inherits AbstractSsh2Connection {
    public {
        #! Connection entry info
        const ConnectionScheme = <ConnectionSchemeInfo>{
            "cls": Class::forName("Ssh2ClientConnection"),
            "options": {
                "keyfile": <ConnectionOptionInfo>{
                    "type": "string",
                    "desc": "a path to a private key file in PEM format to use for key-based authentication",
                },
            },
        };
    }

    #! creates the Ssh2ClientConnection object
    /** @param name the name of the connection
        @param description connection description
        @param url connection URL (potentially with password info)
        @param attributes various attributes. See below
        @param options connection options

        See @ref AbstractConnection::constructor() for \c attributes and \c options reference.
    */
    constructor(string name, string description, string url, hash<auto> attributes = {}, hash<auto> options = {})
        : AbstractSsh2Connection(name, description, url, attributes, options) {
    }

    #! returns \c "ssh2"
    string getType() {
        return "ssh2";
    }

    #! returns a @ref Qore::SSH2::SSH2Client object
    /** @param connect if @ref Qore::True "True", then @ref Qore::SSH2::SSH2Client::connect() is called
        @param rtopts this connection type does not accept any runtime options, so this parameter is ignored

        @return a @ref Qore::SSH2::SSH2Client object
    */
    private Qore::SSH2::SSH2Client getImpl(bool connect = True, *hash<auto> rtopts) {
        SSH2Client sc(url);
        if (opts.keyfile)
            sc.setKeys(opts.keyfile);
        if (connect) {
            sc.connect();
        }
        return sc;
    }

    #! Returns the ConnectionSchemeInfo hash for this object
    private hash<ConnectionSchemeInfo> getConnectionSchemeInfoImpl() {
        return ConnectionScheme;
    }
}

#! class for SFTP connections; returns an object of class @ref Qore::SSH2::SFTPClient for acquiring files from or posting files to SFTP servers
/** supports the following option:
    - \c "keyfile": a path to a private key file in PEM format to use for key-based authentication (environment variables can be prefixed with \c "$" in the path (or optionally enclosed in curly brackets as in \c "${var}") and will be substituted with the value of the environment variable)

    additionally, the following runtime options are supported in getImpl():
    - \c "path": overrides the path component in the URL at runtime
    - \c "path_add": appends the given string to the path component of the URL at runtime
*/
public class SftpConnection inherits AbstractSsh2Connection {
    public {
        #! Connection entry info
        const ConnectionScheme = <ConnectionSchemeInfo>{
            "cls": Class::forName("Ssh2ClientConnection"),
            "options": {
                "keyfile": <ConnectionOptionInfo>{
                    "type": "string",
                    "desc": "a path to a private key file in PEM format to use for key-based authentication",
                },
            },
        };
    }

    #! creates the SftpConnection object
    /** @param name the name of the connection
        @param description connection description
        @param url connection URL (potentially with password info)
        @param attributes various attributes. See below
        @param options connection options

        See @ref AbstractConnection::constructor() for \c attributes and \c options reference.
    */
    constructor(string name, string description, string url, hash<auto> attributes = {}, hash<auto> options = {})
        : AbstractSsh2Connection(name, description, url, attributes, options) {
    }

    #! returns \c "sftp"
    string getType() {
        return "sftp";
    }

    #! returns runtime options
    /** return a hash with the following keys reflecting support for the corresponding runtime options in getImpl():
        - \c "path": overrides the path component in the URL at runtime
        - \c "path_add": appends the given string to the path component of the URL at runtime
    */
    *hash<auto> getRuntimeOptions() {
        return {
            "path": True,
            "path_add": True,
        };
    }

    #! returns a @ref Qore::SSH2::SFTPClient object
    /** @param connect if @ref Qore::True "True", then @ref Qore::SSH2::SFTPClient::connect() is called and also if there is any path in the URL then @ref Qore::SSH2::SFTPClient::chdir() is called on that path
        @param rtopts this object takes the following runtime options:
        - \c "path": overrides the path component in the URL at runtime
        - \c "path_add": appends the given string to the path component of the URL at runtime

        @return a @ref Qore::SSH2::SFTPClient object
    */
    private Qore::SSH2::SFTPClient getImpl(bool connect = True, *hash<auto> rtopts) {
        *string rtpath = getRuntimePath(rtopts);
        SFTPClient sc(url);
        if (opts.keyfile)
            sc.setKeys(opts.keyfile);
        if (connect) {
            sc.connect();
            if (rtpath.val())
                sc.chdir(rtpath);
        }
        return sc;
    }

    private *string getRuntimePath(*hash<auto> rtopts) {
        *string rtpath = rtopts.path ?* urlh.path;
        if (rtopts.path_add.val()) {
            if (rtpath !~ /\/$/ && rtopts.path_add !~ /^\//)
                rtpath += "/";
            rtpath += rtopts.path_add;
        }
        return rtpath;
    }

    #! Returns the ConnectionSchemeInfo hash for this object
    private hash<ConnectionSchemeInfo> getConnectionSchemeInfoImpl() {
        return ConnectionScheme;
    }
}
}
