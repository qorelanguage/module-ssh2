# -*- mode: qore; indent-tabs-mode: nil -*-
#! @file Ssh2Connections.qm SSH2 module connection implementations

/*  Ssh2Connections.qm Copyright 2013 - 2018 Qore Technologies, s.r.o.

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

# minimum required Qore version
%requires qore >= 0.9

# require type definitions everywhere
%require-types

# enable all warnings
%enable-all-warnings

%strict-args

%requires(reexport) ssh2
#%requires Util >= 1.0
%requires(reexport) ConnectionProvider >= 1.1

%new-style

module Ssh2Connections {
    version = "1.1";
    desc = "user module for providing SSH2 connections";
    author = "David Nichols <david@qore.org>";
    url = "http://qore.org";
    license = "MIT";
}

/** @mainpage Ssh2Connections Module

    @tableofcontents

    @section ssh2connectionsintro Introduction to the Ssh2Connections Module

    The %Ssh2Connections module provides connection definitions for classes
    provided by the \c ssh2 module for the @ref connectionproviderintro "ConnectionProvider"
    module in %Qore.

    To use this module, use \c "%requires Ssh2Connections" in your code.

    This module automatically uses the <a href="../../ssh2/html/index.html">ssh2</a> binary module.

    All the public symbols in the module are defined in the Ssh2Connections namespace.

    Currently the module provides the following classes:
    - @ref Ssh2Connections::AbstractSsh2Connection "AbstractSsh2Connection"
    - @ref Ssh2Connections::SftpConnection "SftpConnection"
    - @ref Ssh2Connections::Ssh2ClientConnection "Ssh2ClientConnection"

    @section ssh2connections_relnotes Ssh2Connections Module Release History

    @subsection ssh2connections_v1_1 Ssh2Connections v1.1
    - the @ref Ssh2Connections::Ssh2ClientConnection::getConstructorInfo() "Ssh2ClientConnection::getConstructorInfo()"
      and @ref Ssh2Connections::SftpConnection::getConstructorInfo() "SftpConnection::getConstructorInfo()"
      methods were added to allow connections to be created dynamically, potentially in another process from a network
      call (<a href="https://github.com/qorelanguage/qore/issues/2628">issue 2628</a>)

    @subsection ssh2connections_v1_0 Ssh2Connections v1.0
    - the initial version of the Ssh2Connections module
*/

#! the Ssh2Connections namespace contains all the definitions in the Ssh2Connections module
public namespace Ssh2Connections {
    #! abstract base class for \c ssh module connections
    /** supports the following option:
        - \c "keyfile": a path to a private key file in PEM format to use for key-based authentication
    */
    public class AbstractSsh2Connection inherits ConnectionProvider::AbstractConnection {
        #! DEPRECATED: creates the AbstractSsh2Connection object
        /** @param name the name of the connection
            @param desc connection description
            @param url connection URL
            @param monitor monitoring flag
            @param opts connection options
            @param urlh broken down URL hash (as returned by @ref Qore::parse_url())

            @deprecated since Qore 0.9
        */
        deprecated
        constructor(string name, string desc, string url, bool monitor, *hash opts, hash urlh)
            : AbstractConnection(name, desc, url, {"monitor": monitor}, opts ?? {}) {
        }

        #! creates the AbstractSsh2Connection object
        /** @param name the name of the connection
            @param description connection description
            @param url connection URL (potentially with password info)
            @param attributes various attributes. See below
            @param options connection options

            See @ref AbstractConnection::constructor() for \c attributes and \c options reference.
         */
        constructor(string name, string description, string url, hash attributes = {}, hash options = {})
            : AbstractConnection(name, description, url, attributes, options) {
        }

        #! returns valid options
        /** @return a hash with a single key: \c "keyfile"
        */
        hash getOptions() {
            return {
                "keyfile": True,
            };
        }
    }

    #! class for SSH2 client session connections; returns an object of class @ref Qore::SSH2::SSH2Client
    /** supports the following option:
        - \c "keyfile": a path to a private key file in PEM format to use for key-based authentication (environment variables can be prefixed with \c "$" in the path (or optionally enclosed in curly brackets as in \c "${var}") and will be substituted with the value of the environment variable)
    */
    public class Ssh2ClientConnection inherits AbstractSsh2Connection {
        #! DEPRECATED: creates the Ssh2ClientConnection object
        /** @param name the name of the connection
            @param desc connection description
            @param url connection URL
            @param monitor monitoring flag
            @param opts connection options
            @param urlh broken down URL hash (as returned by @ref Qore::parse_url())

            @deprecated since Qore 0.9
        */
        constructor(string name, string desc, string url, bool monitor, *hash opts, hash urlh)
            : AbstractSsh2Connection(name, desc, url, {"monitor": monitor}, opts ?? {}) {
        }

        #! creates the Ssh2ClientConnection object
        /** @param name the name of the connection
            @param description connection description
            @param url connection URL (potentially with password info)
            @param attributes various attributes. See below
            @param options connection options

            See @ref AbstractConnection::constructor() for \c attributes and \c options reference.
         */
        constructor(string name, string description, string url, hash attributes = {}, hash options = {})
            : AbstractSsh2Connection(name, description, url, attributes, options) {
        }

        #! returns \c "ssh2"
        string getType() {
            return "ssh2";
        }

        #! returns a @ref Qore::SSH2::SSH2Client object
        /** @param connect if @ref Qore::True "True", then @ref Qore::SSH2::SSH2Client::connect() is called
            @param rtopts this connection type does not accept any runtime options, so this parameter is ignored

            @return a @ref Qore::SSH2::SSH2Client object
        */
        private Qore::SSH2::SSH2Client getImpl(bool connect = True, *hash rtopts) {
            SSH2Client sc(url);
            if (opts.keyfile)
                sc.setKeys(opts.keyfile);
            if (connect) {
                sc.connect();
            }
            return sc;
        }

        #! returns a hash that can be used to contruct the object dynamically
        /** @since %Ssh2Connections 1.1
        */
        private hash<ConnectionConstructorInfo> getConstructorInfoImpl() {
            string my_code;
            if (opts.keyfile)
                my_code = sprintf("obj.setKeys(%y); ", opts.keyfile);
            my_code += "if (connect) {obj.connect();}";

            return new hash<ConnectionConstructorInfo>((
                "module": "ssh2",
                "class_name": "Qore::SSH2::SSH2Client",
                "args": url,
                "post_processing": my_code,
            ));
        }

        #! DEPRECATED: static constructor
        /** @deprecated since Qore 0.9 in favor of new constructor and Qore::Reflection
         */
        deprecated
        static SftpConnection make(string name, string desc, string url, bool monitor, *hash opts, hash urlh) {
            return new SftpConnection(name, desc, url, {"monitor": monitor}, opts ?? {});
        }
    }

    #! class for SFTP connections; returns an object of class @ref Qore::SSH2::SFTPClient for acquiring files from or posting files to SFTP servers
    /** supports the following option:
        - \c "keyfile": a path to a private key file in PEM format to use for key-based authentication (environment variables can be prefixed with \c "$" in the path (or optionally enclosed in curly brackets as in \c "${var}") and will be substituted with the value of the environment variable)

        additionally, the following runtime options are supported in getImpl():
        - \c "path": overrides the path component in the URL at runtime
        - \c "path_add": appends the given string to the path component of the URL at runtime
    */
    public class SftpConnection inherits AbstractSsh2Connection {
        #! DEPRECATED: creates the SftpConnection object
        /** @param name the name of the connection
            @param desc connection description
            @param url connection URL
            @param monitor monitoring flag
            @param opts connection options
            @param urlh broken down URL hash (as returned by @ref Qore::parse_url())

            @deprecated since Qore 0.9
        */
        constructor(string name, string desc, string url, bool monitor, *hash opts, hash urlh)
            : AbstractSsh2Connection(name, desc, url, {"monitor": monitor}, opts ?? {}) {
        }

        #! creates the SftpConnection object
        /** @param name the name of the connection
            @param description connection description
            @param url connection URL (potentially with password info)
            @param attributes various attributes. See below
            @param options connection options

            See @ref AbstractConnection::constructor() for \c attributes and \c options reference.
         */
        constructor(string name, string description, string url, hash attributes = {}, hash options = {})
            : AbstractSsh2Connection(name, description, url, attributes, options) {
        }

        #! returns \c "sftp"
        string getType() {
            return "sftp";
        }

        #! returns runtime options
        /** return a hash with the following keys reflecting support for the corresponding runtime options in getImpl():
            - \c "path": overrides the path component in the URL at runtime
            - \c "path_add": appends the given string to the path component of the URL at runtime
        */
        *hash getRuntimeOptions() {
            return (
                "path": True,
                "path_add": True,
                );
        }

        #! returns a @ref Qore::SSH2::SFTPClient object
        /** @param connect if @ref Qore::True "True", then @ref Qore::SSH2::SFTPClient::connect() is called and also if there is any path in the URL then @ref Qore::SSH2::SFTPClient::chdir() is called on that path
            @param rtopts this object takes the following runtime options:
            - \c "path": overrides the path component in the URL at runtime
            - \c "path_add": appends the given string to the path component of the URL at runtime

            @return a @ref Qore::SSH2::SFTPClient object
        */
        private Qore::SSH2::SFTPClient getImpl(bool connect = True, *hash rtopts) {
            *string rtpath = getRuntimePath(rtopts);
            SFTPClient sc(url);
            if (opts.keyfile)
                sc.setKeys(opts.keyfile);
            if (connect) {
                sc.connect();
                if (rtpath.val())
                    sc.chdir(rtpath);
            }
            return sc;
        }

        #! returns a hash that can be used to contruct the object dynamically
        /** @since %Ssh2Connections 1.1
        */
        private hash<ConnectionConstructorInfo> getConstructorInfoImpl() {
            string my_code;
            if (opts.keyfile)
                my_code = sprintf("obj.setKeys(%y); ", opts.keyfile);

             my_code += sprintf("if (connect) {obj.connect(); *string rtpath = rtopts.path ?* %s; if (rtpath.path_add.val()) {if (rtpath !~ /\\/$/ && rtopts.path_add !~ /^\\//) {rtpath += \"/\";} rtpath += rtopts.path_add;} if (rtpath.val()) {obj.chdir(rtpath);}}", urlh.path.val() ? sprintf("%y", urlh.path) : "NOTHING");

            return new hash<ConnectionConstructorInfo>((
                "module": "ssh2",
                "class_name": "Qore::SSH2::SFTPClient",
                "args": url,
                "post_processing": my_code,
            ));
        }

        private *string getRuntimePath(*hash rtopts) {
            *string rtpath = rtopts.path ?* urlh.path;
            if (rtopts.path_add.val()) {
                if (rtpath !~ /\/$/ && rtopts.path_add !~ /^\//)
                    rtpath += "/";
                rtpath += rtopts.path_add;
            }
            return rtpath;
        }

        #! DEPRECATED: static constructor
        /** @deprecated since Qore 0.9 in favor of new constructor and Qore::Reflection
         */
        deprecated
        static SftpConnection make(string name, string desc, string url, bool monitor, *hash opts, hash urlh) {
            return new SftpConnection(name, desc, url, {"monitor": monitor}, opts ?? {});
        }
    }
}
